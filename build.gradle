buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.netflix.nebula:gradle-rxjava-project-plugin:1.12.+'
    }
    configurations.classpath.resolutionStrategy {
        // The above dependency will bring in guava, which then pollutes child projects
        force 'com.google.guava:guava:15.0'

        // The 'nebula-test' plugin depends on Spock which is Groovy version specific (gross!). Explicitly
        // force the version which matches our Groovy version.
        eachDependency { DependencyResolveDetails details ->
            if (details.requested.name == 'spock-core') {
                details.useTarget group: details.requested.group, name: details.requested.name, version: '0.7-groovy-2.0'
            }
        }
    }
}

subprojects {
    buildscript {
        repositories {
            jcenter()
        }
        dependencies {
            classpath 'com.android.tools.build:gradle:0.14.4'
        }
    }
    repositories {
        jcenter()
    }

    apply plugin: 'rxjava-project'
}

apply plugin: 'rxjava-project'
bintrayUpload.enabled = false

ext {
    minSdkVersion = 10
    compileSdkVersion = 21
    buildToolsVersion = '21.1.1'

    rxJava = 'io.reactivex:rxjava:1.0.0'

    junit = 'junit:junit:4.11'
    mockito = 'org.mockito:mockito-core:1.10.8'
    robolectric = 'org.robolectric:robolectric:2.4'
}
